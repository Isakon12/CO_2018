// A grammar for j--, suitable for parsing by recursive descent
// Copyright 2013 Bill Campbell, Swami Iyer and Bahar Akbal-Delibas

compilationUnit ::= [PACKAGE qualifiedIdentifier SEMI]
                    {IMPORT  qualifiedIdentifier SEMI}
                    {typeDeclaration} EOF 

qualifiedIdentifier ::= IDENTIFIER {DOT IDENTIFIER}

typeDeclaration ::= modifiers (classDeclaration | interfaceDeclaration)

modifiers ::= {PUBLIC | PROTECTED | PRIVATE | STATIC | ABSTRACT}

interfaceDeclaration ::= INTERFACE IDENTIFIER
                       [EXTENDS qualifiedIdentifier {COMMA qualifiedIdentifier}] 
                       interfaceBody
                       
interfaceBody ::= LCURLY {modifiers intMemberDecl} RCURLY

IntMemberDecl ::=  (VOID | type) IDENTIFIER  // method
                 formalParameters [THROWS qualifiedIdentifier {COMMA IDENTIFIER}] SEMI
                 | type variableDeclarators SEMI // field

classDeclaration ::= CLASS IDENTIFIER 
                       [EXTENDS qualifiedIdentifier] 
                       [IMPLEMENTS qualifiedIdentifier {COMMA qualifiedIdentifier}]
                       classBody

classBody ::= LCURLY {modifiers memberDecl} RCURLY

memberDecl ::= IDENTIFIER            // constructor
                 formalParameters [THROWS IDENTIFIER {COMMA IDENTIFIER}] block
             | (VOID | type) IDENTIFIER  // method
                 formalParameters [THROWS qualifiedIdentifier {COMMA qualifiedIdentifier}]
                 (block | SEMI)
             | block       
             | type variableDeclarators SEMI // field

block ::= LCURLY {blockStatement} RCURLY 

blockStatement ::= localVariableDeclarationStatement
				 | exceptionThrow
                 | statement
                 
thorwException ::= THORW NEW quealifiedIdentifer [arguments] SEMI

statement ::= block
            | IF parExpression statement [ELSE statement]
            | WHILE parExpression statement
            | FOR forLoop 
            | TRY block CATCH LPAREN formalParameter RPAREN {CATCH LPAREN formalParameter RPAREN}
            	block [FINALLY block]
            | RETURN [expression] SEMI
            | SEMI 
            | statementExpression SEMI

formalParameters ::= LPAREN 
                       [formalParameter {COMMA formalParameter}]
                     RPAREN

formalParameter ::= type IDENTIFIER

parExpression ::= LPAREN expression RPAREN

forLoop ::= LPAREN ((localVariableDeclarationStatement | expression) 
        SEMI expression SEMI statementExpression | type IDENTIFIER COLON IDENTIFIER) 
        RPAREN statement

localVariableDeclarationStatement ::= type variableDeclarators SEMI

loopVariableDeclarationStatement ::= type variableDeclarators


variableDeclarators ::= variableDeclarator {COMMA variableDeclarator}

variableDeclarator ::= IDENTIFIER [ASSIGN variableInitializer]
                    
variableInitializer ::= arrayInitializer | expression

arrayInitializer ::= LCURLY 
                       [variableInitializer {COMMA variableInitializer}]
                     RCURLY 

arguments ::= LPAREN [expression {COMMA expression}] RPAREN

type ::= referenceType | basicType

basicType ::= BOOLEAN | CHAR | INT | DOUBLE

referenceType ::= basicType LBRACK RBRACK {LBRACK RBRACK}
                | qualifiedIdentifier {LBRACK RBRACK}

statementExpression ::= expression // but must have side-effect, eg i++

expression ::= assignmentExpression

assignmentExpression ::= conditionalExpression  // must be a valid lhs level 13
                           [(ASSIGN | PLUS_ASSIGN | MINUS_ASSIGN | STAR_ASSIGN 
                           | DIV_ASSIGN | REM_ASSIGN | ARSHIFT_ASN | ALSHIFT_ASN
                           | LRSHIFT_ASN | EOR_ASSIGN | IOR_ASSIGN | AND_ASSIGN) 
                           assignmentExpression]
                           
conditionalExpression :: = logicalOrExpression // level 12
							{Q_MARK expression COLON expression logicalOrExpression}

logicalOrExpression ::= conditionalAndExpression // level 11
                               {LOR conditionalAndExpression}

conditionalAndExpression ::= inclusiveOrExpression // level 10
                               {LAND inclusiveOrExpression}

inclusiveOrExpression ::= exclusiveOrExpresion // Level 9
					{IOR exclusiveOrExpresion}

exclusiveOrExpression ::= andExpression // level 8
                               {EOR andExpression}                                                              

andExpression ::= equalityExpression // level 7
                               {AND equalityExpression}                               

equalityExpression ::= relationalExpression  // level 6
                         {(EQUAL | NOT_EQUAL) relationalExpression}

relationalExpression ::= shiftExpresion       // level 5
                           [(GT | LE | LT | GE) shiftExpresion
                           | INSTANCEOF referenceType]
                           
shiftExpresion :: = additiveExpresion 			// level 4
							[(ARSHIFT | ALSHIFT | RSHIFT)] additiveExpresion                           

additiveExpression ::= multiplicativeExpression // level 3
                         {(PLUS | MINUS) multiplicativeExpression}

multiplicativeExpression ::= unaryExpression       // level 2
                               {(STAR | DIV | REM) unaryExpression}

unaryExpression ::= (INC | DEC | UCOMP | MINUS | PLUS) unaryExpression  // level 1
                  | simpleUnaryExpression

simpleUnaryExpression ::= LNOT unaryExpression
                        | LPAREN basicType RPAREN unaryExpression //casted
                        | LPAREN // casted               
                            referenceType
                          RPAREN simpleUnaryExpression
                        | postfixExpression 

postfixExpression ::= primary {selector} {(DEC | INC)}

selector ::= DOT qualifiedIdentifier [arguments]
           | LBRACK expression RBRACK

primary ::= parExpression
          | THIS [arguments]
          | SUPER ( arguments 
                  | DOT IDENTIFIER [arguments] 
                  )
          | literal
          | NEW creator
          | qualifiedIdentifier [arguments]

creator ::= (basicType | qualifiedIdentifier) 
              ( arguments
              | LBRACK RBRACK {LBRACK RBRACK} [arrayInitializer]
              | newArrayDeclarator
              )

newArrayDeclarator ::= LBRACK expression RBRACK 
                         {LBRACK expression RBRACK} {LBRACK RBRACK}

literal ::= INT_LITERAL | CHAR_LITERAL | STRING_LITERAL
          | LONG_LITERAL | FLOAT_LITERAL | DOUBLE_LITERAL
          | TRUE        | FALSE        | NULL
